buildscript {
    ext.kotlin_version = "1.0.0-rc-1036"
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    }
}

apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "idea"
apply plugin: "com.github.johnrengelman.shadow"

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    main.java.srcDirs += 'src/main/java'
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    compile group: 'com.google.guava', name: 'guava', version: '18.0'
    compile group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.0'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.9'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.0.4-beta'
    testCompile group: 'ch.qos.logback', name:'logback-classic', version: '1.1.2'
}

shadowJar {
    dependencies {
        exclude(dependency('org.slf4j:slf4j-api:.*'))
        exclude(dependency('com.google.guava:guava:.*'))
    }

    relocate 'kotlin', 'engineer.carrot.warren.warren.repack.kotlin'
    relocate 'com.google.gson', 'engineer.carrot.warren.warren.repack.com.google.gson'
    relocate 'edu.umd.cs.findbugs.annotations', 'engineer.carrot.warren.warren.repack.edu.umd.cs.findbugs.annotations'
    relocate 'javax.annotation', 'engineer.carrot.warren.warren.repack.javax.annotation'
    relocate 'net.jcip.annotations', 'engineer.carrot.warren.warren.repack.net.jcip.annotations'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

def loadProperties(propsFilename) {
    def props = new Properties()
    new File(propsFilename).withInputStream {
        stream -> props.load(stream)
    }
    return new ConfigSlurper().parse(props)
}

def config = loadProperties("build.properties")
def env = System.getenv()

task buildInfo {
    if (env.BUILD_NUMBER != null) {
        ext.build_number = "${env.BUILD_NUMBER}"
    } else {
        def tag_cmd = "git rev-parse --short HEAD"
        def tag_proc = tag_cmd.execute()
        tag_proc.waitFor()
        def branch_cmd = "git branch"
        def branch_proc = branch_cmd.execute()
        branch_proc.waitFor()
        if (tag_proc.exitValue() == 0 && branch_proc.exitValue() == 0) {
            ext.build_number = branch_proc.text.split(' ')[1].trim() + "-" + tag_proc.text.trim()
        } else {
            ext.build_number = "unknown"
        }
    }
}

group = "engineer.carrot.warren"
version = "${config.framework_version}.${project.buildInfo.build_number}"

// Maven deployment
apply plugin: 'maven'

if (!project.hasProperty("DEPLOY_DIR")) {
    ext.DEPLOY_DIR = null
}

artifacts {
    archives shadowJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://${DEPLOY_DIR}")
        }
    }
}

uploadArchives.onlyIf { return DEPLOY_DIR != null }